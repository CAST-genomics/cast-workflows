version 1.0

workflow run_advntr {

    input {
        Array[String] bam_files
        String region
        String google_project
        String gcloud_token
        String vntr_id
    }

    scatter (i in range(length(bam_files))) {
        String bam_file = bam_files[i]

        call download_input {
            input :
                bam_file = bam_file,
                region = region,
                google_project = google_project,
                gcloud_token = gcloud_token,
        }
    
        call genotype {
            input :
                region = region,
                vntr_id = vntr_id,
                target_bam_file = download_input.target_bam_file,
                target_bam_index_file = download_input.target_bam_index_file,
        }

        call sort_index {
            input :
                vcf = genotype.genotype_output
        }
    }

    call merge_outputs {
        input:
            individual_vcfs = sort_index.outvcf,
            individual_vcf_indexes = sort_index.outvcf_index
    }

    output {
        #File target_bam_file = download_input.target_bam_file
        #File target_bam_index_file = download_input.target_bam_index_file

        #Array[File] genotype_output = genotype.genotype_output
        File merged_vcfs = merge_outputs.merged_vcfs
    }

    meta {
        description: "This workflow calls adVNTR to genotype VNTRs"
    }
}

task sort_index {
  input {
    File vcf
  }

  String basename = basename(vcf, ".vcf.gz")

  command <<<
    cat ~{vcf} | vcf-sort | bgzip -c > ~{basename}.sorted.vcf.gz && tabix -p vcf ~{basename}.sorted.vcf.gz
  >>>

  runtime {
        docker:"gcr.io/ucsd-medicine-cast/vcfutils:latest"
        cpu: "1"
        memory: "2G"
    }

  output {
    File outvcf = "${basename}.sorted.vcf.gz"
    File outvcf_index = "${basename}.sorted.vcf.gz.tbi"
  }
}

task merge_outputs {
    input {
        Array[File] individual_vcfs
        Array[File] individual_vcf_indexes
    }

    String out_prefix = "merged_samples"

    command <<<
        mergeSTR --vcfs ~{sep=',' individual_vcfs} --out ~{out_prefix}
    >>>
    runtime {
        docker:"gcr.io/ucsd-medicine-cast/trtools-5.0.1:latest"
        cpu: "1"
        memory: "4G"
    }
    output {
        File merged_vcfs = "~{out_prefix}.vcf"
    }
}

task download_input {
    input {
        String bam_file
        String region
        String google_project
        String gcloud_token
    }

    # Names of all the intermediate files generated to get the target bam file.
    String unsorted_target_bam = "target_region_~{sample_id}_unsorted.bam"
    String sorted_target_bam = "target_~{sample_id}.bam"
    String sorted_target_bam_index = "target_~{sample_id}.bam.bai"
    String sample_id = sub(basename(bam_file), ".bam", "")

    #rm ~{unsorted_target_bam}
    command <<<
        ls -lh .
        export HTSLIB_CONFIGURE_OPTIONS="--enable-gcs"
        export GCS_OAUTH_TOKEN="~{gcloud_token}"
        export GCS_REQUESTER_PAYS_PROJECT="~{google_project}"
        samtools view -hb -o ~{unsorted_target_bam} --use-index ~{bam_file} ~{region}
        samtools sort -o ~{sorted_target_bam} ~{unsorted_target_bam}
        samtools index ~{sorted_target_bam}
        ls -lh .
    >>>

    runtime {
        docker:"sarajava/samtools:1.13"
        cpu: "2"
        memory: "18G"
    }
    output {
        #File? unsorted_target_bam_file = "~{unsorted_target_bam}"
        File target_bam_file = "~{sorted_target_bam}"
        File target_bam_index_file = "~{sorted_target_bam_index}"
    }
}

task genotype {
    input {
        String region
        String vntr_id
        File target_bam_file
        File target_bam_index_file
    }

    # Provide the names of all the output files being generated by AdVNTR including intermediate files.
    String logging = "./log_~{sample_id}.bam.log"
    String filtering_out = "./filtering_out_~{sample_id}.unmapped.fasta.txt"
    String keywords = "./keywords_~{sample_id}.unmapped.fasta.txt"
    String unmapped = "./~{sample_id}.unmapped.fasta"
    String genotype_output = "./~{sample_id}.vcf"
    String sample_id = sub(basename(target_bam_file), ".bam", "")


    # VNTR_db is placed in the docker file. So the path is within the docker image.
    String vntr_db = "/adVNTR/vntr_db/hg38_VNTRs_by_TRF.db"


    # Set VNTR ids for genotyping based on input.
    # Two options right now: VNTR in the ACAN gene or the list of 52 phenotype associated VNTRs.
    #String vids = "$(cat /adVNTR/vntr_db/phenotype_associated_vntrs_comma.txt)"
    #sleep 30

    command <<<
        ls -lh .
        echo "~{target_bam_file}"
        echo "num reads $(samtools view -c ~{target_bam_file})"
        echo "num reads in region $(samtools view -c ~{target_bam_file} chr15:88855424-88857434) "
        /usr/bin/time -v advntr genotype  \
        --alignment_file ~{target_bam_file} \
        --models ~{vntr_db}  \
        --working_directory . \
        -vid ~{vntr_id} \
        --outfmt vcf \
        --log_pacbio_reads \
        --pacbio > ~{genotype_output}
        cat ~{keywords}
        cat ~{filtering_out}
        ls -lh .
    >>>
    #    rm ~{target_bam_file}
    #    rm ~{filtering_out} ~{keywords} ~{unmapped} 

    runtime {
        docker:"sarajava/advntr:1.5.0_db_v3"
        cpu: "2"
        memory: "18G"
    }

    output {
        File? log_file = "~{logging}"
        File? filtering_out = "~{filtering_out}"
        File? keywords = "~{keywords}"
        File genotype_output = "~{genotype_output}"
    }
}
