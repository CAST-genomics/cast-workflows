version 1.0

workflow run_advntr {

    input {
        String bam_file
        String region
        String bam_file_advntr_path
        String output_dir
        String sample_id
        String google_project
        String gcloud_token
    }

    call advntr {
        input :
            bam_file = bam_file,
            region = region,
            bam_file_advntr_path = bam_file_advntr_path,
            output_dir = output_dir,
            sample_id = sample_id,
            google_project = google_project,
            gcloud_token = gcloud_token,
    }

    output {
        File? log_file = advntr.log_file
        File? filtering_out_file = advntr.filtering_out_file
        File? keywords_file = advntr.keywords_file
        File? unmapped_file = advntr.unmapped_file
        File? unsorted_target_bam_file = advntr.unsorted_target_bam_file
        File? sorted_target_bam_file = advntr.sorted_target_bam_file
        File? sorted_target_bam_index_file = advntr.sorted_target_bam_index_file
        File genotype_output = advntr.genotype_output
    }

    meta {
        description: "This workflow calls adVNTR to genotype VNTRs"
    }
}

task advntr {
    input {
        String bam_file
        String region
        String bam_file_advntr_path
        String output_dir
        String sample_id
        String google_project
        String gcloud_token
    }

    parameter_meta {
    }

    # Provide the names of all the output files being generated by AdVNTR including intermediate files.
    String logging = "./log_~{sample_id}.bam.log"
    String filtering_out = "./filtering_out_~{sample_id}.unmapped.fasta.txt"
    String keywords = "./keywords_~{sample_id}.unmapped.fasta.txt"
    String unmapped = "./~{sample_id}.unmapped.fasta"
    String genotype_output = "./~{sample_id}.genotype.vcf"

    # Names of all the intermediate files generated to get the target bam file.
    String unsorted_target_bam = "target_region_~{sample_id}_unsorted.bam"
    String sorted_target_bam = "target_region_~{sample_id}.bam"
    String sorted_target_bam_index = "target_region_~{sample_id}.bam.bai"

    # VNTR_db is placed in the docker file. So the path is within the docker image.
    String vntr_db = "/adVNTR/vntr_db/hg38_VNTRs_by_TRF.db"


    # Set VNTR ids for genotyping based on input.
    # Two options right now: VNTR in the ACAN gene or the list of 52 phenotype associated VNTRs.
    #String vids = "$(cat /adVNTR/vntr_db/phenotype_associated_vntrs_comma.txt)"
    String vids = "290964"

    String samtools_command = "samtools view -hb -o ~{unsorted_target_bam} --use-index ~{bam_file} ~{region}"

    command <<<
        export TMPDIR=/tmp
        export HTSLIB_CONFIGURE_OPTIONS="--enable-gcs"
        export GCS_OAUTH_TOKEN="~{gcloud_token}"
        export GCS_REQUESTER_PAYS_PROJECT="~{google_project}"
        echo ~{samtools_command}
        ~{samtools_command}
        samtools sort -o ~{sorted_target_bam} ~{unsorted_target_bam}
        samtools index ~{sorted_target_bam}
        rm ~{unsorted_target_bam}
        /usr/bin/time -v advntr genotype  \
        --alignment_file ~{bam_file_advntr_path} \
        --models ~{vntr_db}  \
        --working_directory . \
        -vid ~{vids} \
        --outfmt vcf \
        --pacbio > ~{genotype_output}
    >>>

    runtime {
        docker:"sarajava/advntr:1.5.0_db_v2"
        cpu: "4"
        memory: "18G"
    }

    output {
        File? log_file = "~{logging}"
        File? filtering_out_file = "~{filtering_out}"
        File? keywords_file = "~{keywords}"
        File? unmapped_file = "~{unmapped}"
        File? unsorted_target_bam_file = "~{unsorted_target_bam}"
        File? sorted_target_bam_file = "~{sorted_target_bam}"
        File? sorted_target_bam_index_file = "~{sorted_target_bam_index}"
        File genotype_output = "~{genotype_output}"
    }
}
