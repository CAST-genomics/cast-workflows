version 1.0

workflow run_advntr {

    input {
        String bam_file
        String region
        String sample_id
        String google_project
        String gcloud_token
    }

    call download_input {
        input :
            bam_file = bam_file,
            region = region,
            sample_id = sample_id,
            google_project = google_project,
            gcloud_token = gcloud_token,
    }

    call genotype {
        input :
            target_bam_file = download_input.target_bam_file,
            target_bam_index_file = download_input.target_bam_index_file,
            sample_id = sample_id,
    }

    output {
        File target_bam_file = download_input.target_bam_file
        File target_bam_index_file = download_input.target_bam_index_file

        File? log_file = genotype.log_file
        File? filtering_out_file = genotype.filtering_out_file
        File? keywords_file = genotype.keywords_file
        File? unmapped_file = genotype.unmapped_file
        File genotype_output = genotype.genotype_output
    }

    meta {
        description: "This workflow calls adVNTR to genotype VNTRs"
    }
}

task download_input {
    input {
        String bam_file
        String region
        String sample_id
        String google_project
        String gcloud_token
    }

    # Names of all the intermediate files generated to get the target bam file.
    String unsorted_target_bam = "target_region_~{sample_id}_unsorted.bam"
    String sorted_target_bam = "target_region_~{sample_id}.bam"
    String sorted_target_bam_index = "target_region_~{sample_id}.bam.bai"

    command <<<
        ls -lh .
        export HTSLIB_CONFIGURE_OPTIONS="--enable-gcs"
        export GCS_OAUTH_TOKEN="~{gcloud_token}"
        export GCS_REQUESTER_PAYS_PROJECT="~{google_project}"
        samtools view -hb -o ~{unsorted_target_bam} --use-index ~{bam_file} ~{region}
        samtools sort -o ~{sorted_target_bam} ~{unsorted_target_bam}
        samtools index ~{sorted_target_bam}
        rm ~{unsorted_target_bam}
        ls -lh .
    >>>

    runtime {
        docker:"sarajava/advntr:1.5.0_db_u18"
        cpu: "4"
        memory: "18G"
    }
    output {
        File? unsorted_target_bam_file = "~{unsorted_target_bam}"
        File target_bam_file = "~{sorted_target_bam}"
        File target_bam_index_file = "~{sorted_target_bam_index}"
    }
}

task genotype {
    input {
        File target_bam_file
        File target_bam_index_file
        String sample_id
    }

    # Provide the names of all the output files being generated by AdVNTR including intermediate files.
    String logging = "./log_~{sample_id}.bam.log"
    String filtering_out = "./filtering_out_~{sample_id}.unmapped.fasta.txt"
    String keywords = "./keywords_~{sample_id}.unmapped.fasta.txt"
    String unmapped = "./~{sample_id}.unmapped.fasta"
    String genotype_output = "./~{sample_id}.genotype.vcf"


    # VNTR_db is placed in the docker file. So the path is within the docker image.
    String vntr_db = "/adVNTR/vntr_db/hg38_VNTRs_by_TRF.db"


    # Set VNTR ids for genotyping based on input.
    # Two options right now: VNTR in the ACAN gene or the list of 52 phenotype associated VNTRs.
    #String vids = "$(cat /adVNTR/vntr_db/phenotype_associated_vntrs_comma.txt)"
    String vids = "290964"

    command <<<
        ls -lh .
        /usr/bin/time -v advntr genotype  \
        --alignment_file ~{target_bam_file} \
        --models ~{vntr_db}  \
        --working_directory . \
        -vid ~{vids} \
        --outfmt vcf \
        --pacbio > ~{genotype_output}
        rm ~{target_bam_file}
        ls -lh .
    >>>

    runtime {
        docker:"sarajava/advntr:1.5.0_db_v2"
        cpu: "4"
        memory: "18G"
    }

    output {
        File? log_file = "~{logging}"
        File? filtering_out_file = "~{filtering_out}"
        File? keywords_file = "~{keywords}"
        File? unmapped_file = "~{unmapped}"
        File genotype_output = "~{genotype_output}"
    }
}
